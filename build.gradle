buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

version = '0.0.1-1.19.2'
group = 'net.cwjn.idf' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'idf'

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: '1.19.2'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg') // Currently, this location cannot be changed from the default.
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '-mixin.config=idf.mixin.json'
            mods {
                idf {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '-mixin.config=idf.mixin.json'
            mods {
                idf {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'idf', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'), '-mixin.config=idf.mixin.json'
            mods {
                idf {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

def gpr_creds = {
    username = property('gpr.username')
    password = property('gpr.token')
}

repositories {
    maven {
        url "https://cursemaven.com"
    }
    maven {
        name = "blamejared maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        url = uri("https://maven.pkg.github.com/silentchaos512/silent-gear")
        credentials gpr_creds
    }
    maven {
        url = uri("https://maven.pkg.github.com/silentchaos512/silentlib")
        credentials gpr_creds
    }
    maven {
        url = uri("https://maven.pkg.github.com/silentchaos512/silent-utils")
        credentials gpr_creds
    }
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.19.2-43.2.3'

    //STAPLES
    // compile against the JEI API but do not include it at runtime
    compileOnly(fg.deobf("mezz.jei:jei-1.19.2-common-api:11.2.0.246"))
    compileOnly(fg.deobf("mezz.jei:jei-1.19.2-forge-api:11.2.0.246"))
    // at runtime, use the full JEI jar for Forge
    runtimeOnly(fg.deobf("mezz.jei:jei-1.19.2-forge:11.2.0.246"))
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.19.2-5.1.1.0")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.19.2-5.1.1.0:api")
    compileOnly fg.deobf("vazkii.patchouli:Patchouli:1.19.2-76:api")
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:1.19.2-76")

    //LEGENDARY TOOLTIPS + DEPENDENCIES
    implementation fg.deobf("curse.maven:iceberg-520110:4068355")
    implementation fg.deobf("curse.maven:itemframes-532127:4068347")
    implementation fg.deobf("curse.maven:prism-638111:3993293")

    //UPDATE TETRA MICK I BEG U BRO
    //implementation fg.deobf("se.mickelus.mutil:mutil:1.18.2-4.5.0")
    //implementation fg.deobf("se.mickelus.tetra:tetra:1.18.2-4.9.2")

    //SILENTGEAR + SILENTLIB
    implementation fg.deobf("net.silentchaos512:silent-lib:${project.sl_version}") {
        exclude module: "forge"
    }
    implementation fg.deobf("net.silentchaos512:silent-gear:${project.sgear_version}") {
        exclude module: 'forge'
        exclude module: 'jei-1.19.2-forge'
        exclude module: 'silent-lib'
        exclude module: 'curios-forge'
        exclude module: 'World-Stripper'
        exclude module: 'Patchouli'
    }

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    // Real mod deobf dependency examples - these get remapped to your current mappings
    // compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api") // Adds JEI API as a compile dependency
    // runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}") // Adds the full JEI mod as a runtime dependency
    // implementation fg.deobf("com.tterrag.registrate:Registrate:MC${mc_version}-${registrate_version}") // Adds registrate as a dependency

}

mixin {
    add sourceSets.main, "idf.mixin-refmap.json"
    config 'idf.mixin.json'
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : "Improved Damage Framework",
                "Specification-Vendor"    : "cwjn",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "cwjn",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": 0,
                "MixinConfigs" : "idf.mixin.json"
        ])
    }
}

// Example configuration to allow publishing using the maven-publish plugin
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
// publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}
